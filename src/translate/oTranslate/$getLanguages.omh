
Calculate lcURI as con('https://translation.googleapis.com/language/translate/v2/languages?key=',cAPIKey)
If len(pcTargetLang)
	Calculate lcURI as con(lcURI,'&target=',pcTargetLang)
End If

Do iorefRest.$init(lcURI,kOWEBhttpMethodGet,ilHeaders,'') Returns lbOK

Do iorefRest.$run() Returns lbOK
If not(lbOK)
	Calculate pcLangs as 'call to Google failed.'
	Quit method kFalse
End If

Do iorefRest.$returnVal(irResponseHeaders,irResponse,irJSON) Returns icJSON

If irResponse.httpStatusCode<>200
	Calculate pError as irJSON.error
	Calculate lbOK as kFalse
Else
	Calculate lrResult as OJSON.$jsontolistorrow(icJSON)
	Calculate lrResult as lrResult.C1
	Calculate lrResult as lrResult.C1
	
	Do llLangs.$cols.$add('language',kCharacter,kSimplechar,10)
	If len(pcTargetLang)
		Do llLangs.$cols.$add('name',kCharacter,kSimplechar,50)
	End If
	For lnCol from 1 to lrResult.$colcount step 1
		Calculate lrLang as lrResult.___[lnCol]
		If len(pcTargetLang)
			Do llLangs.$add(lrLang.language,lrLang.name)
		Else
			Do llLangs.$add(lrLang.language)
		End If
	End For
	Calculate pcLangs as llLangs
End If

Quit method lbOK